apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.abyu"
archivesBaseName = "tide"
version = "1.0.0"

repositories {
  mavenCentral()
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile 'org.mockito:mockito-all:1.9.5'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if(project.hasProperty('ossrhUsername')){
signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Tide'
        packaging 'jar'
        description 'Event based pub-sub framework for Java' 
        url 'http://abyu.github.io/tide' 

        scm {
          connection 'git@github.com:abyu/tide.git'
          developerConnection 'git@github.com:abyu/tide.git'
          url 'https://github.com/abyu/tide/' 
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        dependencies {
        }

        developers {
          developer {
            id 'abyu'
            name 'Kishore Kumar'
            email 'abyu.kishore@gmail.com'
          }
        }
      }
    }
  }

  } 
} 
